-------------------------------------------------------------------------
-- Design unit: R8 simutation test bench
-- Description: R8 processor connected to a RAM memory
--      The RAM memory is able to load image files generated by the R8 simulator             
-------------------------------------------------------------------------

library IEEE;
use IEEE.std_logic_1164.all;  
use IEEE.std_logic_arith.CONV_STD_LOGIC_VECTOR;
use ieee.std_logic_unsigned.all; 	-- CONV_INTEGER function	
USE ieee.numeric_std.ALL;

entity R8_uC is	
	generic(
		DATA_WIDTH  		: integer := 16;        					-- Data bus width 
		ADDR_WIDTH  		: integer := 16;        					-- Address bus width
		IMAGE       		: string := "teste_io_fpga_BRAM.txt"    	-- Memory content to be loaded    (text file)
	);
    port( 
        clk     			: in std_logic;
        rst     			: in std_logic;
        port_io				: inout std_logic_vector(DATA_WIDTH-1 downto 0)		-- External interface
    );
end R8_uC;    

architecture arch1 of R8_uC is
    
	signal clk_div2			: std_logic;
	signal clk_div2_n		: std_logic;
    signal rw, wr_n 		: std_logic;
	signal ce				: std_logic;
	signal ce_memory		: std_logic;
	signal ce_peripheral	: std_logic_vector(7 downto 0);
	
	signal dataR8_out		: std_logic_vector(15 downto 0);
	signal dataR8_in		: std_logic_vector(15 downto 0);
	signal dataMem_out		: std_logic_vector(15 downto 0);
	signal dataP1_out		: std_logic_vector(15 downto 0);
	signal addressR8 		: std_logic_vector(15 downto 0);    
    
begin			 
	-- Clock signal 
	clk_div2_n	<= not clk_div2; 
	
	-- Memory & Peripheral read/write    																						 
    wr_n <= not rw; 											-- R8 			( write->rw=0 read->rw=1 )		
																-- Memory 		( write->wr=1 read->wr=0 )
															   	-- Peripheral	( write->wr=1 read->wr=0 )
																
	--========================================================================================================
	-- DCM
	--========================================================================================================
	DCM: entity work.ClockManager  								-- DCM spartan 6 	100MHz(In) -> 50MHz(Out)
		port map(			   	
			clk_in			=> clk,
			clk_div2		=> clk_div2
		);
		
	--========================================================================================================
	-- CPU R8
	--========================================================================================================
    PROCESSOR: entity work.R8 
		generic map(
			DATA_WIDTH  => DATA_WIDTH,         					-- Data bus width
        	ADDR_WIDTH  => ADDR_WIDTH          					-- Address bus width
		)
        port map (
            clk         	=> clk_div2, 
            rst         	=> rst, 
            data_in     	=> dataR8_in, 
            data_out    	=> dataR8_out, 
            address     	=> addressR8, 
            ce          	=> ce, 
            rw          	=> rw
        );
		
	dataR8_in	<= dataP1_out when addressR8(15 downto 12) = "1000" else 		-- MUX data_in
				   dataMem_out;
	--========================================================================================================
	-- CE Decoder
	--========================================================================================================
	CEDEC: entity work.AddressDecoder 
		generic map(
			DATA_WIDTH  		=> DATA_WIDTH          				-- Data bus width	  
		)
		port map(
			address 			=> addressR8,
			ce					=> ce,								-- CE from CPU
			ce_memory			=> ce_memory,					 	-- CE to memory
			ce_peripheral		=> ce_peripheral				  	-- CE to peripherals, which bit is one peripheral
		);
		
	--========================================================================================================
	-- Memory
	--========================================================================================================
    RAM: entity work.Memory   
        generic map (			
			DATA_WIDTH  		=> DATA_WIDTH,         				-- Data bus width			 	2 bytes word
        	ADDR_WIDTH  		=> 15,         						-- Address bus width  			32768 positions
        	IMAGE       		=> IMAGE    						-- Memory content to be loaded  (text file)																											 
        )
        port map (
            clk     			=> clk_div2_n,
            wr    				=> wr_n,							-- Write Enable (1: write; 0: read) 
			en    				=> ce_memory,						-- Memory enable
          	address 			=> addressR8(14 downto 0),			-- 
			data_in				=> dataR8_out,						-- Data from CPU
			data_out			=> dataMem_out				  		-- Data to CPU
        );	  			      										

	--========================================================================================================
	-- I/O Port
	--========================================================================================================
	PORT1: entity work.BidirectionalPort
		generic map(
	        DATA_WIDTH          => DATA_WIDTH,    					-- Port width in bits
	        PORT_DATA_ADDR      => "10",     						-- Address of data register? 	= 2 
	        PORT_CONFIG_ADDR    => "01",							-- Address of config register  	= 1
	        PORT_ENABLE_ADDR    => "00"								-- Address of enable register  	= 0
	    )
	    port map(  
	        clk         		=> clk_div2,
	        rst         		=> rst,
	        data_i      		=> dataR8_out,						-- Data from CPU
	        data_o      		=> dataP1_out,					 	-- Data to CPU
	        address    			=> addressR8(1 downto 0),
	        rw          		=> wr_n,							-- 0: read; 1: write
	        ce          		=> ce_peripheral(0),			   	-- Peripheral "ID" = 0
	        port_io     		=> port_io							-- Data to external interface
	    );
				
end arch1;